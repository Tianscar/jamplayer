plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

ext {
    libraryName = 'Java Audio Media Player'
    libraryGroupName = 'com.tianscar.jamplayer'
    libraryVendorName = 'Tianscar'

    libraryVersionCode = 3
    libraryVersionName = '1.1.0'

    librarySourceCompatibility = JavaVersion.VERSION_1_8
    libraryTargetCompatibility = JavaVersion.VERSION_1_8
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = librarySourceCompatibility
targetCompatibility = libraryTargetCompatibility

group = libraryGroupName
version = libraryVersionName

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.tianscar.javasound:javasound-resloader:0.1.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    testImplementation 'com.tianscar.javasound:javasound-flac:1.4.1'
    testImplementation 'com.tianscar.javasound:javasound-mp3:1.9.8'
    testImplementation 'com.tianscar.javasound:javasound-shorten:3.6.2'
    testImplementation 'com.tianscar.javasound:javasound-speex:0.9.8'
    testImplementation 'com.tianscar.javasound:javasound-vorbis:2.0.0'
    testImplementation 'com.tianscar.javasound:javasound-wavpack:1.4.2'
    testImplementation 'com.tianscar.javasound:javasound-aac:0.9.8'
    testImplementation 'com.tianscar.javasound:javasound-alac:0.2.3'
    testImplementation 'com.tianscar.javasound:javasound-ape:1.7.7'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = libraryGroupName
            artifactId = rootProject.name
            version = libraryVersionName
            from components.java
            pom {
                name = libraryName
                description = 'Java Audio Media Player'
                url = 'https://github.com/Tianscar/jamplayer'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Tianscar'
                        name = 'Karstian Lee'
                        email = 'tianscar@protonmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com/Tianscar/jamplayer.git'
                    developerConnection = 'scm:git:git@github.com/Tianscar/jamplayer.git'
                    url = 'https://github.com/Tianscar/jamplayer'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}